plugins {
    id "maven-publish"
    id "idea"
}

//Print out JVM information so that we know what version is running. Extreamly useful for people to know when helping you.
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

def isCI = System.getenv("GITHUB_ACTION")
def isRELEASE = System.getenv("GITHUB_RELEASE")
def gitHash() {
    String hash = System.getenv("GITHUB_SHA")
    if (hash != null) return hash.substring(0,8)
    return ""
}

subprojects{
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: "idea"
    group = rootProject.maven_group
    version = "${project.mod_version}-${rootProject.minecraft_version}"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    apply from: 'https://raw.githubusercontent.com/GregTech-Intergalactical/GradleSripts/main/repositories.gradle'

    java {
        withSourcesJar()
    }
    if (isCI) {
        if (!isRELEASE){
            version = version + "-" + gitHash()
            println("Not in CI Release mode")
        }
        println("In CI mode")
    }
    publishing {
        publications {
            mavenJava(org.gradle.api.publish.maven.MavenPublication) {
                if (project.name != "forge"){
                    artifactId = "structurelib-" + project.name
                    from components.java
                }
            }
        }

        repositories {
            if (isCI && isRELEASE) {
                maven {
                    url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
                    credentials {
                        username = System.getenv("MAVEN_USERNAME")
                        password = System.getenv("MAVEN_PASSWORD")
                    }
                }
            } else {
                maven { url "file:///${project.projectDir}/mcmodsrepo"}
            }
        }
    }
}
